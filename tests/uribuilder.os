Перем юТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт

	юТест = ЮнитТестирование;

	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_ПостроитьИдентификаторРесурса");

	Возврат ВсеТесты;

КонецФункции

Процедура ТестДолжен_ПостроитьИдентификаторРесурса() Экспорт

	// Конструктор без параметров
	Построитель = Новый ПостроительURI;
	Построитель.Хост = "localhost";
	юТест.ПроверитьРавенство(Построитель.URI, "http://localhost/", "http по-умолчанию");

	Построитель.Порт = 21;
	юТест.ПроверитьРавенство(Построитель.URI, "http://localhost:21/", "Нестандартный порт");

	Построитель.Схема = "ftp";
	юТест.ПроверитьРавенство(Построитель.URI, "ftp://localhost/", "Стандартный порт не http");

	Построитель.ИмяПользователя = "user";
	юТест.ПроверитьРавенство(Построитель.URI, "ftp://user@localhost/");

	Построитель.Схема = "https";
	Построитель.Порт = 443;
	юТест.ПроверитьРавенство(Построитель.URI, "https://user@localhost/");

	Построитель.Пароль = "pass";
	юТест.ПроверитьРавенство(Построитель.URI, "https://user:pass@localhost/");

	Построитель.Путь = "path/to/resourse.os";
	юТест.ПроверитьРавенство(Построитель.URI, "https://user:pass@localhost/path/to/resourse.os");

	Построитель.Запрос = "Параметр1=Строка%20С%20Пробелом&Параметр2=1";
	юТест.ПроверитьРавенство(Построитель.URI,
		"https://user:pass@localhost/path/to/resourse.os?Параметр1=Строка С Пробелом&Параметр2=1",
		"Парамеры кириллицей с пробелами");

	Построитель.Запрос = "";
	Построитель.Фрагмент = "Фрагмент";
	юТест.ПроверитьРавенство(Построитель.URI,
		"https://user:pass@localhost/path/to/resourse.os#Фрагмент",
		"Фрагмент");

КонецПроцедуры

